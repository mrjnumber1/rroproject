// Badges Exchange
// *********************************************************************
bat_room,174,141,3	script	BG Rental Tops	861,{
	if (islowratechar() == 1)
	{
		mes "This function is only available to BG chars!";
		close;
	}
	vending_open();
}
bat_room,173,144,3	script	BG Rental Mids	721,{
	if (islowratechar() == 1)
	{
		mes "This function is only available to BG chars!";
		close;
	}
	vending_open();
}
bat_room,172,147,3	script	BG Rental Lower	860,{
	if (islowratechar() == 1)
	{
		mes "This function is only available to BG chars!";
		close;
	}
	vending_open();
}
bat_room,177,147,3	script	BG Rental Armors	117,{
	if (islowratechar() == 1)
	{
		mes "This function is only available to BG chars!";
		close;
	}
	vending_open();
}
bat_room,180,148,3	script	BG Rental Shields	116,{
	if (islowratechar() == 1)
	{
		mes "This function is only available to BG chars!";
		close;
	}
	vending_open();
}
bat_room,183,147,3	script	BG Rental Shoes	115,{
	if (islowratechar() == 1)
	{
		mes "This function is only available to BG chars!";
		close;
	}
	vending_open();
}
bat_room,186,147,3	script	BG Rental Garments	114,{
	if (islowratechar() == 1)
	{
		mes "This function is only available to BG chars!";
		close;
	}
	vending_open();
}
bat_room,188,144,3	script	BG Rental Accessories	113,{
	if (islowratechar() == 1)
	{
		mes "This function is only available to BG chars!";
		close;
	}
	vending_open();
}
bat_room,189,141,3	script	BG Rental Weapons	112,{
	if (islowratechar() == 1)
	{
		mes "This function is only available to BG chars!";
		close;
	}
	vending_open();
}


bat_room,142,140,5	script	Tierra BG Items	419,{
	if (islowratechar() == 1)
	{
		mes "This function is only available to BG chars!";
		close;
	}
	vending_open();
}
bat_room,137,140,5	script	Flavius BG Items	413,{
	if (islowratechar() == 1)
	{
		mes "This function is only available to BG chars!";
		close;
	}
	vending_open();
}
bat_room,140,145,5	script	KvM BG Items	420,{
	if (islowratechar() == 1)
	{
		mes "This function is only available to BG chars!";
		close;
	}
	vending_open();
}
bat_room,148,150,5	script	Kafra Point Exchanger	894,{
	if (islowratechar() == 1)
	{
		mes "This function is only available to BG chars!";
		close;
	}
	set .@n$, "[Kafra Point Exchanger]";
	set .cq_required, 10;
	set .kp_bonus, 100;
	
	
	mes .@n$;
	mes "If you have enough Conquest Participation points, I can give you Kafra Points in exchange.";
	mes "^0000CF"+.cq_required+"^000000 Conquests = ^AFAF00"+.kp_bonus+"^000000 Kafra Points";
	mes "You have ^FF0000"+Conquest_Participation+"^000000 Conquest Points";
	
	if (select("Cancel:Exchange for Kafra Points")==1)
		close;
		
	
	set .@gained, Conquest_Participation / .cq_required;
	next;
	if (.@gained <= 0)
	{
		mes .@n$;
		mes "Ah, I'm sorry, you do not have enough Conquest Participation points";
		close;
	}
	
	set Conquest_Participation, Conquest_Participation - (Conquest_Participation * .@gained);
	givepoints(.@gained* .kp_bonus);
	
	mes .@n$;
	mes "You now have: ^FF0000"+Conquest_Participation+"^000000 Conquest Points.";
	mes "Have a nice day!";
	close;
	
	
}


-	script	f_bg_offical_items	-1,{
end;
OnInit:
	function add_bg_item;
	
	set .bg_items_loaded, 0;

	
	set .@npc, 0;	
		set .cost, 100;
			add_bg_item(.@npc, 13036);add_bg_item(.@npc, 13411);add_bg_item(.@npc,  1425);add_bg_item(.@npc,  1632);
			add_bg_item(.@npc,  1634);add_bg_item(.@npc,  1543);add_bg_item(.@npc,  1924);add_bg_item(.@npc,  1978);
			add_bg_item(.@npc,  1574);add_bg_item(.@npc,  1824);add_bg_item(.@npc,  1183);add_bg_item(.@npc,  1380);
			add_bg_item(.@npc, 13305);add_bg_item(.@npc,  1279);add_bg_item(.@npc,  1739);add_bg_item(.@npc, 13108);
			add_bg_item(.@npc, 13172);
		
	set .@npc, 1;
		set .cost, 100;
			add_bg_item(.@npc, 13037);add_bg_item(.@npc, 13410);add_bg_item(.@npc,  1633);add_bg_item(.@npc,  1635);
			add_bg_item(.@npc,  1542);add_bg_item(.@npc,  1923);add_bg_item(.@npc,  1977);add_bg_item(.@npc,  1575);
			add_bg_item(.@npc,  1823);add_bg_item(.@npc,  1184);add_bg_item(.@npc,  1482);add_bg_item(.@npc,  1379);
			add_bg_item(.@npc, 13306);add_bg_item(.@npc,  1280);add_bg_item(.@npc,  1738);add_bg_item(.@npc, 13171);
			add_bg_item(.@npc, 13173);
			add_bg_item(.@npc, 13174);
	
	
	// these all belong in flavius and tierra
	set .@npc, 0;
	set .cost, 500;
	for(set .@i, 0; .@i < 2; set .@i, .@i + 1)
	{	
		set .cost, 50;
			add_bg_item(.@npc+.@i,  2538);add_bg_item(.@npc+.@i,  2539);add_bg_item(.@npc+.@i,  2540);add_bg_item(.@npc+.@i,  2435);
			add_bg_item(.@npc+.@i,  2436);add_bg_item(.@npc+.@i,  2437);
		set .cost, 80;
			add_bg_item(.@npc+.@i,  2376);add_bg_item(.@npc+.@i,  2377);add_bg_item(.@npc+.@i,  2378);add_bg_item(.@npc+.@i,  2379);
			add_bg_item(.@npc+.@i,  2380);add_bg_item(.@npc+.@i,  2381);add_bg_item(.@npc+.@i,  2382);
		set .cost, 500;
			add_bg_item(.@npc+.@i,  2720);add_bg_item(.@npc+.@i,  2721);add_bg_item(.@npc+.@i,  2722);add_bg_item(.@npc+.@i,  2723);
			add_bg_item(.@npc+.@i,  2724);add_bg_item(.@npc+.@i,  2725);add_bg_item(.@npc+.@i,  2733);
	}



 
	set .@npc, 2;
		set .cost, 500;
			add_bg_item(.@npc,  1187);add_bg_item(.@npc,  1281);add_bg_item(.@npc,  1282);add_bg_item(.@npc,  1310);
			add_bg_item(.@npc,  1282);add_bg_item(.@npc,  1426);add_bg_item(.@npc,  1486);add_bg_item(.@npc,  1546);
			add_bg_item(.@npc,  1576);add_bg_item(.@npc,  1577);add_bg_item(.@npc,  1640);add_bg_item(.@npc,  1641);
			add_bg_item(.@npc,  1743);add_bg_item(.@npc,  1826);add_bg_item(.@npc,  1827);add_bg_item(.@npc,  1927);
			add_bg_item(.@npc,  1981);add_bg_item(.@npc,  2002);add_bg_item(.@npc, 13042);add_bg_item(.@npc, 13110);
			add_bg_item(.@npc, 13176);add_bg_item(.@npc, 13177);add_bg_item(.@npc, 13178);add_bg_item(.@npc, 13179);
			add_bg_item(.@npc, 13307);add_bg_item(.@npc, 13416);add_bg_item(.@npc, 13417);add_bg_item(.@npc, 13418);

			add_bg_item(.@npc, 2394, 840);add_bg_item(.@npc, 2395, 170);add_bg_item(.@npc, 2396,  25);add_bg_item(.@npc, 2444, 580);
			add_bg_item(.@npc, 2445,  70);add_bg_item(.@npc, 2446,  15);add_bg_item(.@npc, 2549, 630);add_bg_item(.@npc, 2772,1200);
			add_bg_item(.@npc, 2773, 280);add_bg_item(.@npc, 2774,  40);
		
	
	set .bg_items_loaded, 1;
	set .cost, 0;
	function add_bg_item {
		if (.bg_items_loaded != 0)
			return;
			
		set .@exchanger, getarg(0);
		set .@id, getarg(1);
		set .@cost, getarg(2, .cost);
		
		set .@name$, "";
		
		switch (.@exchanger)
		{
			case 0:
				set .@name$, "Tierra BG Items";
				break;
			case 1:
				set .@name$, "Flavius BG Items";
				break;
			case 2:
				set .@name$, "KvM BG Items";
				break;
			default:
				debugmes "Invalid exchanger id given! ID: "+.@exchanger;
				return;
		}
		
		vending_add(.@id, .@cost, 7773, 0, 0, 0, 0, 0, 0, 0, .@name$);
	}
}

// Boss Cat!!
bat_room,180,136,3	script	Kafra Corp. Rep	894,{
	set .@n$, "[Kafra Corp. Rep]";
	mes .@n$;
	mes "Welcome to the Kafra Rentals Marketplace. We have a special contract with Maroll to exchange Battle Badges for Battlegrounds Gears specially crafted by Mann Co. Engineers to match anything that young adventurers have been able to find.";
	next;
	mes .@n$;
	mes "Please know that these items are all temporary per the conditions of our contract. We will provide you the best services our contract allows.";
	close;
}

// Skill/Stat Reset

bat_room,168,142,3	script	Mann Co. Sorceror	64,{

	if (islowratechar() == 1)
	{
		mes "This function is only available to BG chars!";
		close;
	}
	
	mes "[Mann Co. Sorceror]";
	mes "I am a man whose mystical powers can reset your skill and stat point allocations.";
	mes "Would you like to do it?";
	
	if ( select("No! Let me be!:Reset Stats and Skills") == 1)
	{
		mes "[Mann Co. Sorceror]";
		mes "Aye then, well, be sure to visit me if you our your friends need my assistance. Fight on, great warrior!";
		close;
	}
	
	next;
	
	mes "[Mann Co. Sorceror]";
	mes "And what will you reset?";
	next;
	switch ( select("None! Cancel!:Stats:Skills:Both at once") )
	{
		case 1:
			mes "[Mann Co. Sorceror]";
			mes "Oh, I see..";
			close;
			break;
		case 2:
			mes "[Mann Co. Sorceror]";
			mes "It shall be done.";
			ResetStatus;
			close;
			break;
		case 3:
			mes "[Mann Co. Sorceror]";
			mes "It shall be done.";
			ResetSkill;
			close2;
			force_disconnect();
			break;
		case 4:
			mes "[Mann Co. Sorceror]";
			mes "It shall be done.";
			ResetSkill;
			ResetStatus;
			close2;
			force_disconnect();
			break;
			
	}
	
}

// Weapon Upgrader448
//
bat_room,169,145,6	script	Mann Co. Refiner	63,{
	if (islowratechar() == 1)
	{
		mes "This function is only available to BG chars!";
		close;
	}
	set .@n$, "[Mann Co. Refiner]";
	mes .@n$;
	mes "I'm the armsmith who can upgrade your items, for only 10 Kafra Points per level. What would you like me to refine?";
	next;
	
	setarray .@position$[1], "Head","Body","Left Hand","Right Hand","Robe","Shoes";
	set .@menu$,"";
	for( set .@i,1; .@i <= 6; set .@i,.@i+1 )
	{
		if( getequipisequiped(.@i) )
			set .@menu$, .@menu$ + .@position$[.@i] + "-" + "[" + getequipname(.@i) + "]";

		set .@menu$, .@menu$ + ":";
	}
	set .@part,select(.@menu$);
	if(!getequipisequiped(.@part)) 
	{
		mes .@n$;
		mes "You're not wearing anything there that I can refine.";
		emotion e_an;
		close;
	}
	//Check if the item is refinable...
	if(!getequipisenableref(.@part)) 
	{
		mes .@n$;
		mes "I don't think I can refine this item at all...";
		close;
	}
	//Check to see if the items is already +10
	if(getequiprefinerycnt(.@part) >= 10) {
		mes .@n$;
		mes "I can't refine this any more. This is as refined as it gets!";
		close;
	}
	
	set .@refineitemid, getequipid(.@part); // save id of the item
	set .@refinerycnt, getequiprefinerycnt(.@part); //save refinery count
	set .@price, 10;
	switch(getequipweaponlv(.@part))
	{
	case 0: 	//Refine Armor
		set .@safe,4;
		break;
	case 1: 	//Refine Level 1 Weapon
		set .@safe,7; 
		break;
	case 2: 	//Refine Level 2 Weapon
		set .@safe,6; 
		break;
	case 3: 	//Refine Level 3 Weapon
		set .@safe,5; 
		break;
	case 4: 	//Refine Level 4 Weapon
		set .@safe,4; 
		break;
	case 5: 	//Refine other stuff?
		set .@safe,4; 
		break;
	}
	
	
	if(getequiprefinerycnt(.@part) < .@safe) 
	{
		mes .@n$;
		mes "I can refine this to the safe limit or a desired number of times... it's your choice...";
		next;
		set .@menu2,select("To the safe limit please.","I'll decide how many times.","I've changed my mind...");
	} 
	else 
		set .@menu2,2;
	switch(.@menu2)
	{
	case 1: 
		set .@refinecnt,.@safe - getequiprefinerycnt(.@part);
		break;
	case 2:
		next;
		mes .@n$;
		mes "So how many times would you like me to refine your item?";
		next;
		input .@refinecnt;
		set .@refinecheck,.@refinecnt + getequiprefinerycnt(.@part);
		if (.@refinecnt < 1 || .@refinecheck > 10) 
		{
			mes .@n$;
			mes "I can't refine this item that many times.";
			close;
		}
		if(.@refinecheck > .@safe) 
		{
			set .@refinecheck,.@refinecheck - .@safe;
			mes .@n$;
			mes "This will try to refine the equipment " + .@refinecheck + " times past the safe limit. Your equipment may be destroyed... is that ok?";
			next;
			if(select("Yes...","No...") == 2)
			{
				mes .@n$;
				mes "You said so..Hmm so be it...";
				close;
			}
		}
		break;
	case 3:
		next;
		mes .@n$;
		mes "You said so..Hmm so be it...";
		close;
	}
	set .@fullprice,.@price * .@refinecnt;
	mes .@n$;
	mes "That will cost you " + .@fullprice + " Kafra Points. Is that ok?";
	next;
	if(select("Yes","No...") == 2){
		mes .@n$;
		mes "You said so..Hmm so be it...";
		close;
	}
	if(##KAFRAPOINTS < .@fullprice) 
	{
		mes .@n$;
		mes "Is that all you got? Unfortunately I can't work for you at a lower price. Try putting yourself in my shoes.";
		close;
	}
	paypoints(.@fullprice);

	while(.@refinecnt)
	{
		if (getequipisequiped(.@part) == 0) {
			mes .@n$;
			mes "Look here... you don't have any Items on...";
			close;
		}
		if (getequipid(.@part) != .@refineitemid || (.@menu2 == 1 && getequippercentrefinery(.@part) < 100)) 
		{
			mes .@n$;
			mes "Clan... No, but Did you imagine I could be so stupid !?!";
			mes "You have changed it...";
			mes "Go out before I stun you with my Hammer!!!";
			close;
		} 
		mes "Clang, clang!!!";
		if(.@menu2 == 2 && getequippercentrefinery(.@part) <= rand(100)) 
		{
			failedrefitem .@part;
			emotion e_omg;
			mes .@n$;
			mes "WAHHHH!!! I'm so sorry... I warned you this could happen...";
			set .@refinecnt,.@refinecnt - 1;
			if(.@refinecnt == 0) close;
			mes "Here's the unused Kafra Points back...";
			set .@fullprice,.@refinecnt * .@price;
			givepoints(.@fullprice);

			close;
		}
		successrefitem .@part;
		emotion e_no1;
		set .@refinecnt,.@refinecnt - 1;
		next;
	}
	mes .@n$;
	mes "All finished... Come again soon.";
	close;		
	
}
bat_room,150,161,2	script	Saxton Hale	731,{
	if (islowratechar() == 1)
	{
		mes "This function is only available to BG chars!";
		close;
	}
	mes "[Saxton Hale]";
	mes "What do you need?";
	mes "Remember, these items go Directly to Storage.";
	switch(select("BG Supply:WoE Supply:WoE Boxes:BG Ammo:WoE Ammo"))
	{
	case 1:
		setarray .@Items[0], 11501,11502, 547, 505, 526, 14509, 14510, 14511, 678, 7135, 7136, 7137, 7138, 7139,  715,  716,  717,12016, 1025, 7321,  662,12073,12077,12082,12087,12093,12098, 12114, 12115, 12116, 12117, 12020, 12118, 12119, 12120, 12121, 12319, 12320, 14536, 605, 606, 12029, 12124, 682, 683, 12033, 12034, 12299, 14544, 14542, 12274, 12275, 14530, 12247, 12351, 12298, 12018;
		
		for (set .@i, 0; .@i < getarraysize(.@Items); set .@i, .@i + 1)
		{
			set .@Amount[.@i], 1000;
			set .@Value[.@i], 1;
		}
		//setarray .@Amount[0], 10000,  10000,  10000,  10000, 10000, 10000,  10000,  1000,  1000,  1000,  1000,  1000,  1000,  1000,  1000,   10000,   10000,  1000,  1000,    1000,    1000,    1000,    1000,    1000,    1000,    10000,    10000,    10000,    10000,    10000,    10000,    10000,    10000,    10000,     1000,    1000,    1000,    1000,  1000,  1000,    1000,    1000,  1000,  1000,    1000,    1000,    1000,     1000,     1000,     1000,     1000,     1000,     1000,    1000,     1000,     1000,     1000,    1000;  
		//setarray .@Value[0],     1,    1,    1,    1,   1,   1,    1,    1,    1,    1,    1,    1,    1,    1,    1,    1,    1,    1,    1,    1,    1,    1,    1,    1,     1,     1,     1,     1,     1,     1,     1,     1,     1,     1,     1,     1,     1,   1,   1,     1,     1,   1,   1,     1,     1,     1,     1,     1,     1,     1,     1,     1,     1,     1,     1,     1,     1;
		set .@cID, getbattleflag("bg_reserved_char_id");
		set .@Tarjet$, "BG";
		break;
	case 2: // WoE Supply Items
		setarray .@Items[0],  11501, 11502, 526,14509,14510,14511,   7137, 7138,   662,12073,12077,12082,12087,12093,12098, 12114, 12115, 12116, 12117, 12020, 12118, 12119, 12120, 12121,  12319, 12320, 14536, 605, 606, 12029, 12124, 682, 683, 12033, 12034, 12299, 14544, 14542, 12274, 12275, 14530, 12247, 12351, 12298, 12018;
		setarray .@Amount[0],  1000,  1000, 150,   50,   50,   50,    100,  100,   100,    5,    5,    5,    5,    5,    5,    50,    50,    50,    50,    50,    50,    50,    50,   50,      30,    30,    30,  30,  30,    30,    30,  30,  30,    30,    30,     5,     5,     5,     1,     1,    30,     1,     1,     1,    20;   
		//setarray .@Value[0],    1, 1, 1,   1,   1,   1,  1,  1,   1,   1,   1,   1,   1,   1,   1,   1,   1,   1,   1,   1,   1,   1,   1,   1,   1,   1,    1,    1,    1,    1,    1,    1,    1,    1,    1,    1,    1,    1,    1,  1,  1,    1,    1,  1,  1,    10;
		
		for (set .@i, 0; .@i < getarraysize(.@Items); set .@i, .@i + 1)
		{
			set .@Value[.@i], 1;
		}
		set .@cID, getbattleflag("woe_reserved_char_id");
		set .@Tarjet$, "WoE";
		break;
	case 3: // WoE Boxes
		setarray .@Items[0],  8916, 8917, 8924, 8929, 8935, 8934,  8926, 8927, 8962, 8961, 8925, 8928, 8930;
		setarray .@Amount[0],    3,    1,    2,    2,    1,    1,     2,    1,    1,    1,    4,    1,    1;
		setarray .@Value[0],     2,    2,    2,    2,    2,    2,     2,    2,    2,    2,    2,    2,    2;
		set .@cID, 0;
		set .@Tarjet$, "WoE";
		break;
	case 4: // BG Arrows
		setarray .@Items[0], 12004,12005,12006,12009,12008,12012,12010,12011,12014,12015,12013,12007,12183,1773,1774,1758,1759,1761,1763,1764,1766,1768,1769,13200,13201,13202,13203,13204,13205,13206,13207,13250,13251,13252,13253,13254,7521,7522,7523,7524,1771;
		setarray .@Amount[0],5, 5, 5, 5, 5, 5, 5, 5, 5, 5, 5, 5, 5,2500,2500,2500,2500,2500,2500,2500,2500,2500,2500,2500,2500,2500,2500,2500,2500,2500,2500,2500,2500,2500,2500,2500,2500,2500,2500,2500,2500;
		setarray .@Value[0], 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1,   1,   1,   1,   1,   1,   1,   1,   1,   1,   1,   1,   1,   1,   1,   1,   1,   1,   1,   1,   1,    1, 1,   1,   1,   1,   1,   1,   1;
		set .@cID, getbattleflag("bg_reserved_char_id");
		set .@Tarjet$, "BG";
		break;
	case 5: // WoE  Arrows
		setarray .@Items[0], 12004,12005,12006,12009,12008,12012,12010,12011,12014,12015,12013,12007,12183,1773,1774,1758,1759,1761,1763,1764,1766,1768,1769,13200,13201,13202,13203,13204,13205,13206,13207,13250,13251,13252,13253,13254,7521,7522,7523,7524,1771;
		setarray .@Amount[0],5, 5, 5, 5, 5, 5, 5, 5, 5, 5, 5, 5, 5,2500,2500,2500,2500,2500,2500,2500,2500,2500,2500,2500,2500,2500,2500,2500,2500,2500,2500,2500,2500,2500,2500,2500,2500,2500,2500,2500,2500;
		setarray .@Value[0], 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1,   1,   1,   1,   1,   1,   1,   1,   1,   1,   1,   1,   1,   1,   1,   1,   1,   1,   1,   1,   1,    1, 1,   1,   1,   1,   1,   1,   1;
		set .@cID, getbattleflag("woe_reserved_char_id");
		set .@Tarjet$, "WoE";
		break;
		
		
	//case 5: // Raid Supply Items
	//	setarray .@Items[0],   547,11501,11502,  525, 526, 678, 7135, 7136, 7137, 7138, 7139,  715,  716,  717,12016, 1025, 7321,  662,12073,12077,12082,12087,12093,12098, 12114, 12115, 12116, 12117, 12020, 12118, 12119, 12120, 12121, 12279, 12319, 12320, 14536, 605, 606, 12028, 12029, 682, 683, 12032, 12033, 12034, 12299, 14544, 14542, 12354, 12274, 12275, 14530, 12247, 12351, 12298, 12018;
	//	setarray .@Amount[0],  200,  200,  200,   50,  50,  50,  100,  100,  100,  100,  400,  100,  200,  150,   50,   50,  100,  100,    5,    5,    5,    5,    5,    5,    50,    50,    50,    50,    50,    50,    50,    50,    50,     5,    30,    30,    30,  30,  30,    30,    30,  30,  30,    30,    30,    30,     5,     5,     5,     5,     1,     1,    30,     1,     1,     1,    20;   
	//	setarray .@Value[0],     1,    1,    1,    1,   1,   1,    1,    1,    1,    1,    1,    1,    1,    1,    1,    1,    1,    1,    1,    1,    1,    1,    1,    1,     1,     1,     1,     1,     1,     1,     1,     1,     1,     1,     1,     1,     1,   1,   1,     1,     1,   1,   1,     1,     1,     1,     1,     1,     1,     1,     1,     1,     1,     1,     1,     1,     1;

	//	set .@cID, getbattleflag("raid_reserved_char_id");
	//	set .@Tarjet$, "Raid";
	//	break;
	}
	set .@Menu$, "";
	for( set .@i, 0; .@i < getarraysize(.@Items); set .@i, .@i + 1 )
		set .@Menu$, .@Menu$ + .@Amount[.@i] + " " + getitemname(.@Items[.@i]) + " (" + .@Value[.@i] + " badges):";
	set .@j, select(.@Menu$) - 1;
	mes "[Saxton Hale]";
	mes "^0000FF" + .@Amount[.@j] + " ^nItemID^" +.@Items[.@j] + "^000000";
	mes "It will cost you " + .@Value[.@j] + " badges per pack.";
	mes "It will be found in your storage";
	set .@Destiny, 1;

	mes "How many packages do you want to buy? (1-30)";
	next;

	input .@total;
	if( .@total < 1 || .@total > 30)
	{
		mes "[Saxton Hale]";
		mes "I am sorry, please input a value from 1 to 30.";
		mes "Talk to me again when you do your selections.";
		close;
	}
	
	set .@vtotal, .@total * .@Value[.@j]; // Value of all Packages
	set .@total, .@total * .@Amount[.@j]; // Total Amount of Items

	if( .@Destiny )
	{ // Check Storage Space Limit
		if( .@cID )
		{
			if( checkspace(.@Items[.@j],.@total,1,0,0,254,0,.@cID&0xffff,(.@cID>>16)&0xffff) == 0 )
			{
				mes "[Saxton Hale]";
				mes "It's seen your storage cannot accept more items.";
				mes "Go to your store and check the free space.";
				close;
			}
		}
		else
		{
			if( checkspace(.@Items[.@j],.@total,1,0,0,0,0,0,0) == 0 )
			{
				mes "[Saxton Hale]";
				mes "It's seen your storage cannot accept more items.";
				mes "Go to your store and check the free space.";
				close;
			}
		}
	}
	else
	{ // Check Weight Limit
		if( checkweight(.@Items[.@j],.@total) == 0 )
		{
			mes "[Saxton Hale]";
			mes "It's seen you can't carry the stuff you want to buy.";
			mes "Go and store some items on your Kafra Storage.";
			close;
		}
	}

	if( (countitem(7773)) < .@vtotal )
	{
		mes "[Saxton Hale]";
		mes "Required Badges: ^0000FF" + .@vtotal + "^000000";
		mes "You do not have enough badges. Sorry...";
		mes "Participate in Battleground arenas to receive it.";
		close;
	}
	
	set .@i7773, .@vtotal;

	//mes "[Saxton Hale]";
	//mes "Are you ready to pay and receive your items?";
	//mes "^0000FF" + .@i7773 + "^000000 Battle Badges";

	//if( select("Yes:Nevermind") == 2 )
	//{
	//	mes "[Saxton Hale]";
	//	mes "Ok, come again if you change your mind.";
	//	close;
	//}

	if( .@Destiny )
	{ // Check Storage Space Limit
		if( .@cID )
		{
			if( checkspace(.@Items[.@j],.@total,1,0,0,254,0,.@cID&0xffff,(.@cID>>16)&0xffff) == 0 )
			{
				mes "[Saxton Hale]";
				mes "It seems your storage cannot accept more items.";
				mes "Go to your storage and clean it";
				close;
			}
		}
		else
		{
			if( checkspace(.@Items[.@j],.@total,1,0,0,0,0,0,0) == 0 )
			{
				mes "[Saxton Hale]";
				mes "It seems your storage cannot accept more items.";
				mes "Go to your storage and clean it.";
				close;
			}
		}
	}
	else
	{ // Check Weight Limit
		if( checkweight(.@Items[.@j],.@total) == 0 )
		{
			mes "[Saxton Hale]";
			mes "It seems you can't carry the stuff you want to buy.";
			mes "Go and store some items!";
			close;
		}
	}

	if( .@i7773 > countitem(7773) )
	{
		mes "[Saxton Hale]";
		mes "What!?, Are you a hacker?";
		mes "If you believe you are seeing this message in error, contact a GM.";
		announce "Saxton Hale: Hey everyone! "+strcharinfo(0)+" just tried to exploit a bug! Boo this man! BOOOO!!!!", bc_all;
		close;
	}

	delitem 7773,.@i7773;

	if( .@cID )
	{
		if( .@Destiny )
			storeitem2 .@Items[.@j],.@total,1,0,0,254,0,.@cID&0xffff,(.@cID>>16)&0xffff;
		else
			getitem2 .@Items[.@j],.@total,1,0,0,254,0,.@cID&0xffff,(.@cID>>16)&0xffff;
	}
	else
	{
		if( .@Destiny )
			storeitem .@Items[.@j],.@total;
		else
			getitem .@Items[.@j],.@total;
	}
	
	next;
	mes "[Saxton Hale]";
	mes "Okay, the items have been placed in your Storage.";
	mes "Come back to me if you need more items, brave warrior!";
	close;
}




// Hidding Slot Enchant NPC
// Adapted from old ChronosRO script
// Important Note : Item ID 677 Platinum Coin is a special item that allow user to select the Enchant, from old chronosRO

bat_room,166,145,4	script	Mann Co. Engineer	448,{

	if (islowratechar() == 1)
	{
		mes "This function is only available to BG chars!";
		close;
	}
	mes "[Mann Co. Engineer]";
	mes "I'm a man who can apply special bonuses to your equipment in exchange for BG badges";
	mes "How can I help you?";
	next;
	switch( select( "^4169E1Weapon Enchant^000000:Enchant Armor:^0000FFHeadGear Enchant^000000:^FFA500Garment/Footgear Enchant^000000:^FF0000Shield Enchant^000000:^4169E1Accesory Enchant^000000:Information:Cancel" ) )
	{
		case 1:
			mes "[Mann Co. Engineer]";
			mes "When you enchant your weapon, your weapon will game 5% attack and magical damage against opponents, depending on what outcome you get. You may repeat the process as much as you want.";
			mes "However, please be aware the weapon will lose its refines and cards.";
			mes "Also, you may not enchant weapons with more than 3 slots.";
			next;
			mes "[Mann Co. Engineer]";
			mes "^FFA500To enchant your Weapon^000000:";
			mes "a) 50 Battle Badges.";
			mes "b) 30 Kafra Points";
			next;

			setarray .@Position$[3], "Left hand","Right hand";
			set .@Menu$,"";
			for( set .@i, 3; .@i <= 4; set .@i, .@i + 1 )
			{
				if( getequipisequiped(.@i) )
					set .@Menu$, .@Menu$ + .@Position$[.@i] + "-" + "[" + getequipname(.@i) + "]";
				set .@Menu$, .@Menu$ + ":";
			}

			set .@Part,select(.@Menu$ + "Cancel") + 2;
			if( .@Part >= 5 )
			{
				mes "[Mann Co. Engineer]";
				mes "Need some time to think about it, huh?";
				mes "Fair enough.";
				close;
			}
			if( !getequipisequiped(.@Part) )
			{
				mes "[Mann Co. Engineer]";
				mes "You're not wearing anything there...";
				close;
			}
			if(!getequipisenableref(.@Part)) 
			{
				mes "[Mann Co. Engineer]";
				mes "Oh... this item is not refineable, I'm sorry.";
				close;
			}
			if( getequipweaponlv(.@Part) == 0 )
			{
				mes "[Mann Co. Engineer]";
				mes "This enchant only works for Weapons, not Shields...";
				close;
			}
			set .@Slot0, getequipcardid(.@Part,0);
			if( .@Slot0 == 255 || .@Slot0 == 254 || .@Slot0 < 0 )
			{
				mes "[Mann Co. Engineer]";
				mes "I am sorry, I cannot work on signed items.";
				close;
			}
			set .@Item, getequipid(.@Part);
			if( getitemslots(.@Item) >= 4 )
			{
				mes "[Mann Co. Engineer]";
				mes "I am sorry, I cannot work on 4 slotted weapons.";
				close;
			}

			mes "[Mann Co. Engineer]";
			mes "You want to enchant your " + getitemname(.@Item) + "?";
			mes "The cost of this is ^0000FF50 Battle Badges, and 30 Kafra Points^000000.";
			mes "But before that, I must warn you of the risk.";
			mes "Once the weapon receives the enchant ^FF0000it will lose refine and cards^000000.";
			mes "This process cannot destroy your weapon, so you may proceed safely.";
			next;
			if( select("^FF0000Elemental^000000:^0000FFRacial^000000:Cancel") == 3 )
			{
				mes "[Mann Co. Engineer]";
				mes "Need some time to think about it, huh?";
				mes "Fair enough.";
				close;
			}

			set .@Type, @menu; // 1 : Elemental | 2 : Racial
			if( countitem(7773) < 50 || ##KAFRAPOINTS < 30 )
			{
				mes "[Mann Co. Engineer]";
				mes "I'd like to go ahead with this Enchant attempt, but you're missing a few things.";
				mes "You sure that you have the badges?";
				close;
			}

			set .@Slot0, getequipcardid(.@Part,0);
			if( !getequipisequiped(.@Part) || !getequipweaponlv(.@Part) || .@Slot0 == 255 || .@Slot0 == 254 || .@Slot0 < 0 || .@Item != getequipid(.@Part) )
			{
				mes "[Mann Co. Engineer]";
				mes "This is not the equip we were talking about...";
				close;
			}

			switch( .@Type )
			{
			case 1: // Elemental
				set .@Enchant, 4901 + rand(5);
				break;
			case 2: // Racial
				set .@Enchant, 4906 + rand(10);
				break;
			}

			delitem 7773,50;
			paypoints 30;

			successenchant .@Part,.@Enchant;

			mes "[Mann Co. Engineer]";
			mes "Great, your weapon received ^0000FF" + getitemname(.@Enchant) + "^000000.";
			mes "It looks pretty well done.";
			mes "Come back again if you want something else.";
			close;
		case 2:			
			if( !getequipisequiped(2) )
			{
				mes "[Mann Co. Engineer]";
				mes "You're not wearing any armor...";
				mes "I can't enchant your body!";
				close;
			}
				
			deletearray .@Armor[0],127;			
			setarray .@Armor[0],2358,2307,2309,2314,2316,2321,2325,2327,2330,2332,2334,2335,2341,2344,2346,2348,2350,2337,2386,2311,2318,2319,2320,2308,2310,2315,2317,2322,2324,2326,2331,2333,2336,2342,2345,2347,2349,2351,2357,2364,2365,2374,2375,2387,2389,2391,2390,2376,2377,2378,2379,2380,2381,2382,2394;

			set .@Item, getequipid(2);
			
			for( set .@i, 0; .@i < getarraysize(.@Armor); set .@i, .@i + 1 )
			{
				if( .@Armor[.@i] == .@Item )
					break;
			}

			if( .@i >= getarraysize(.@Armor) )
			{
				mes "[Mann Co. Engineer]";
				mes "I am sorry, but I cannot work on this armor.";
				mes "If you need information, just ask me...";
				close;
			}
			if(!getequipisenableref(2)) 
			{
				mes "[Mann Co. Engineer]";
				mes "Oh... this item is not refineable, I'm sorry.";
				close;
			}

			mes "[Mann Co. Engineer]";
			mes "You want to enchant your " + getitemname(.@Item) + "?";
			mes "The cost of my work is 30 Battle Badges";
			mes "^FF0000Your Armor will lose any Refine and Cards^000000.";
			mes "Also, if the attempt to Enchant fails, the ^FF0000Armor^000000 will be destroyed^000000.";
			next;
			if ( select("Do the enchant","Cancel") == 2 )
			{
				mes "[Mann Co. Engineer]";
				mes "Need some time to think about it, huh?";
				mes "Okay, I understand.";
				close;
			}

			if( getequipisequiped(2) == 0 || getequipid(2) != .@Item || countitem(7773) < 30 )
			{
				mes "[Mann Co. Engineer]";
				mes "I'd like to go ahead with this Enchant attempt, but you're missing a few things.";
				mes "You sure that you have the equipment and the badges?";
				close;
			}

			mes "[Mann Co. Engineer]";
			mes "Alright then, let the work begin!";
			mes "You'd better pray for a successful result.";
			mes "Let me take your armor...";
			next;
			set .@Enchant, 0;

			delitem(7773, 30);

			if( .@Enchant == 0 )
			{
				set .@Rate, rand(100);
				
				if( .@Rate >= 85) 		// 15% chance for stat+3
					set .@Enchant, 4702 + (rand(6) * 10);
				else if (.@Rate >= 55) 	// 30% chance for stat+2
					set .@Enchant, 4701 + (rand(6) * 10);
				else if (.@Rate >= 15) 	// 40% chance for stat+1
					set .@Enchant, 4700 + (rand(6) * 10);
				else 					// 15% chance for failure 
				{
					failedenchant 2, 0;
					emotion e_sob;
					mes "[Mann Co. Engineer]";
					mes "Wah! ...I am so sorry, it failed.";
					mes "Better luck next time!";
					close;
				}
			}


			successenchant 2,.@Enchant;
			emotion e_no1;

			mes "[Mann Co. Engineer]";
			mes "Great, it was a success.";
			mes "Your armor received ^0000FF" + getitemname(.@Enchant) + "^000000 enchant.";
			mes "Congratulations!";
			mes "See you again, buddy!";
			close;
		case 3:
			mes "[Mann Co. Engineer]";
			mes "^FFA500To enchant your HeadGear^000000:";
			mes "a) 200 Battle Badges.";
			mes "b) 100 Kafra Points"; 
			mes "You will gain +1 to a random stat.";
			mes "^FF0000Headgear will lose refines and cards^000000.";
			next;
			if ( select("Attempt Enchant","Cancel") == 2 )
			{
				mes "[Mann Co. Engineer]";
				mes "Need some time to think about it, huh?";
				mes "Okay, I understand.";
				close;
			}
			if( !getequipisequiped(1) )
			{
				mes "[Mann Co. Engineer]";
				mes "You're not wearing any headgear...";
				mes "I can't enchant your dumb brain!";
				close;
			}
			
			set .@Slot0, getequipcardid(1,0);
			set .@Slot3, getequipcardid(1,3);
			set .@NeedCP, 0;
			
			if( .@Slot0 == 255 || .@Slot0 == 254 || .@Slot0 < 0 )
			{
				mes "[Mann Co. Engineer]";
				mes "I am sorry, I cannot work on signed items.";
				close;
			}
			if(!getequipisenableref(1)) 
			{
				mes "[Mann Co. Engineer]";
				mes "Oh... this item is not refineable, I'm sorry.";
				close;
			}
			
	//		if( .@Slot3 != 0 )
	//		{
	//			set .@NeedCP, 1;
	//			mes "[Mann Co. Engineer]";
	//			mes "This headgear is already enchanted. You will need also to pay me 1 ^0000FFTerra's Bronze Coin^000000.";
	//			next;
	//		}

			mes "[Mann Co. Engineer]";
			mes "You want to enchant your " + getitemname(getequipid(1)) + "?";
			mes "The cost of this work is ^0000FF200 Battle Badges and 100 Kafra Points^000000.";
			mes "Once the headgear receives the enchant, ^FF0000it will lose refine and cards^000000.";
			mes "Do you still want to Enchant?";
			next;
			if ( select("Let's do it!","Cancel") == 2 )
			{
				mes "[Mann Co. Engineer]";
				mes "Need some time to think about it, huh?";
				mes "Okay, I understand.";
				close;
			}

			if( countitem(7773) < 200 || ##KAFRAPOINTS < 100 )
			{
				mes "[Mann Co. Engineer]";
				mes "I'd like to go ahead with this Enchant attempt, but you're missing a few things.";
				mes "You sure that you have the badges and Kafra Points?";
				close;
			}

			set .@Slot0, getequipcardid(1,0);
			set .@Slot3, getequipcardid(1,3);

			if( !getequipisequiped(1) || .@Slot0 == 255 || .@Slot0 == 254 || .@Slot0 < 0 )
			{
				mes "[Mann Co. Engineer]";
				mes "This is not the headgear we were talking about...";
				mes "Get out of here!!";
				close;
			}

			set .@Enchant, 0;

			delitem 7773,200;
			paypoints 100;
			if( .@Enchant == 0 ) set .@Enchant, 4768 + (rand(6) * 3);
		//	if( .@NeedCP ) delitem 8905,1;
			successenchant 1,.@Enchant;

			mes "[Mann Co. Engineer]";
			mes "Great, your headgear received ^0000FF" + getitemname(.@Enchant) + "^000000 enchant.";
			mes "It looks pretty well done. Congratulations!";
			mes "See you again, buddy!";
			close;
		case 4:
			mes "[Mann Co. Engineer]";
			mes "^FFA500To enchant Footgear or Garment.^000000:";
			mes "a) 30 Battle Badges.";
			mes "b) 10 Kafra Points.";
			mes "You will receive an HP or SP bonus";
			mes "^FF0000Equip will loose refines and cards^000000.";
			next;
			if ( select("Enchant my Garment:Enchant my Footgear:Cancel") == 3 )
			{
				mes "[Mann Co. Engineer]";
				mes "Need some time to think about it, huh?";
				mes "Okay, I understand.";
				close;
			}
			
			set .@Equip, 4 + @menu; // Selected option
			if( !getequipisequiped(.@Equip) )
			{
				mes "[Mann Co. Engineer]";
				mes "You're not wearing nothing there...";
				mes "Your body is not ready!";
				close;
			}
			if(!getequipisenableref(.@Equip)) 
			{
				mes "[Mann Co. Engineer]";
				mes "Oh... this item is not refineable, I'm sorry.";
				close;
			}

			set .@Slot0, getequipcardid(.@Equip,0);
			if( .@Slot0 == 255 || .@Slot0 == 254 || .@Slot0 < 0 )
			{
				mes "[Mann Co. Engineer]";
				mes "I am sorry, I can't work on signed items.";
				close;
			}

			mes "[Mann Co. Engineer]";
			mes "You want to enchant your " + getitemname(getequipid(.@Equip)) + "?";
			mes "The cost of this work is ^0000FF30 Battle Badges and 10 Kafra Points^000000.";
			mes "But before that, I must warn you of the risk.";
			mes "Once the equip receives the enchant ^FF0000it will lose refine and cards^000000.";
			mes "Do you still want to Enchant?";
			next;
			if ( select("^FFFF00Enchant it with HP^000000:^0000FFEnchant it with SP^000000:Cancel") == 3 )
			{
				mes "[Mann Co. Engineer]";
				mes "Need some time to think about it, huh?";
				mes "Okay, fine fine...";
				close;
			}

			set .@Type, @menu - 1; // 0 : HP | 1 : SP
			if( countitem(7773) < 30 || ##KAFRAPOINTS < 10 )
			{
				mes "[Mann Co. Engineer]";
				mes "I'd like to go ahead with this Enchant attempt, but you're missing a few things.";
				mes "You sure that you have the badges and Kafra Points?";
				close;
			}

			set .@Slot0, getequipcardid(.@Equip,0);
			if( !getequipisequiped(.@Equip) || .@Slot0 == 255 || .@Slot0 == 254 || .@Slot0 < 0 )
			{
				mes "[Mann Co. Engineer]";
				mes "This is not the equip we were talking about...";
				mes "Get out of here!";
				close;
			}

			set .@Rate, rand(100);
			
			if( .@Rate >= 85 )
				set .@Enchant, 4934 + (.@Type * 4); // 15% chance of best
			else if ( .@Rate >= 60 )
				set .@Enchant, 4933 + (.@Type * 4); // 25% chance of betterer
			else if ( .@Rate >= 35 )
				set .@Enchant, 4932 + (.@Type * 4); // 25% chance of better but shit
			else
				set .@Enchant, 4931 + (.@Type * 4); // 35% chance of kinda shitty
				
	//		}

			delitem 7773,30;
			paypoints 10;

			successenchant .@Equip,.@Enchant;

			mes "[Mann Co. Engineer]";
			mes "Great, your equip received ^0000FF" + getitemname(.@Enchant) + "^000000 enchant.";
			mes "It looks pretty good. Congratulations!";
			mes "See you again, buddy!";
			close;
		case 5:
			mes "[Mann Co. Engineer]";
			mes "^FFA500To enchant a Shield you need:^000000:";
			mes "a) 30 Battle Badges.";
			mes "b) 10 Kafra Points";
			mes "You gain 5% Elemental or Racial resistance.";
			mes "^FF0000Shield will lose refines and cards^000000.";
			next;
			if ( select("Attempt enchant:Cancel") == 2 )
			{
				mes "[Mann Co. Engineer]";
				mes "Need some time to think about it, huh?";
				mes "That's okay.";
				close;
			}

			if( !getequipisequiped(3) )
			{
				mes "[Mann Co. Engineer]";
				mes "You're not wearing nothing anything...";
				mes "I can't fix your hand, but now you've made my pimp hand strong!";
				close;
			}
			
			if( getequipweaponlv(3) )
			{
				mes "[Mann Co. Engineer]";
				mes "This enchant is for Shields, not weapons...";
				close;
			}
			if(!getequipisenableref(3)) 
			{
				mes "[Mann Co. Engineer]";
				mes "Oh... this item is not refineable, I'm sorry.";
				close;
			}

			set .@Slot0, getequipcardid(3,0);
			if( .@Slot0 == 255 || .@Slot0 == 254 || .@Slot0 < 0 )
			{
				mes "[Mann Co. Engineer]";
				mes "I am sorry, I can't work on signed items.";
				close;
			}

			mes "[Mann Co. Engineer]";
			mes "You want to enchant your " + getitemname(getequipid(3)) + "?";
			mes "The cost of this work is ^0000FF30 Battle Badges and 10 Kafra Points^000000.";
			mes "I must warn you of the risk.";
			mes "Once the equip receives the enchant ^FF0000it will lose refine and cards^000000.";
			mes "Do you still want to Enchant?";
			next;
			if ( select("^FFFF00Enchant it Elemental^000000:^0000FFEnchant it Racial^000000:Cancel") == 3 )
			{
				mes "[Mann Co. Engineer]";
				mes "Need some time to think about it, huh?";
				mes "That's fine!";
				close;
			}

			set .@Type, @menu; // 1 : Elemental | 2 : Racial
			if( countitem(7773) < 30 || ##KAFRAPOINTS < 10)
			{
				mes "[Mann Co. Engineer]";
				mes "I'd like to go ahead with this Enchant attempt, but you're missing a few things.";
				mes "You sure that you have the badges?";
				close;
			}

			set .@Slot0, getequipcardid(3,0);
			if( !getequipisequiped(3) || getequipweaponlv(3) || .@Slot0 == 255 || .@Slot0 == 254 || .@Slot0 < 0 )
			{
				mes "[Mann Co. Engineer]";
				mes "This is not the equip we were talking about...";
				mes "Get out of here!!";
				close;
			}


			switch( .@Type )
			{
			case 1: // Elemental
				set .@Enchant, 4916 + rand(5);
				break;
			case 2: // Racial
				set .@Enchant, 4921 + rand(10);
				break;
			}

			delitem 7773,30;
			paypoints 10;

			successenchant 3,.@Enchant;

			mes "[Mann Co. Engineer]";
			mes "Great, your equip received ^0000FF" + getitemname(.@Enchant) + "^000000 enchant.";
			mes "It looks pretty well done. Congratulations!";
			next;
			mes "[Mann Co. Engineer]";
			mes "See you again, buddy!";
			close;
		case 6:
			mes "[Mann Co. Engineer]";
			mes "^FFA500To enchant your Accesory^000000:";
			mes "a) 200 Battle Badges.";
			mes "c) 100 Kafra Points";
			mes "Enchant Type is a Bonus on Max Weight";
			mes "^FF0000Accesory will lose cards^000000.";
			next;

			setarray .@Position$[7], "Accessory 1","Accessory 2";
			set .@Menu$,"";
			for( set .@i, 7; .@i <= 8; set .@i, .@i + 1 )
			{
				if( getequipisequiped(.@i) )
					set .@Menu$, .@Menu$ + .@Position$[.@i] + "-" + "[" + getequipname(.@i) + "]";
				set .@Menu$, .@Menu$ + ":";
			}

			set .@Part,select(.@Menu$ + "Cancel") + 6;
			if( .@Part >= 9 )
			{
				mes "[Mann Co. Engineer]";
				mes "Need some time to think about it, huh?";
				mes "Alright, I can understand.";
				mes "Just remember that life's no fun if you're always playing it safe~";
				close;
			}
			if( !getequipisequiped(.@Part) )
			{
				mes "[Mann Co. Engineer]";
				mes "You're not wearing anything there...";
				close;
			}
			if(!getequipisenableref(.@Part)) 
			{
				mes "[Mann Co. Engineer]";
				mes "Oh... this item is not refineable, I'm sorry.";
				close;
			}

			set .@Slot0, getequipcardid(.@Part,0);
			if( .@Slot0 == 255 || .@Slot0 == 254 || .@Slot0 < 0 )
			{
				mes "[Mann Co. Engineer]";
				mes "I am sorry, i cannot work on Signed items.";
				close;
			}

			set .@Item, getequipid(.@Part);

			mes "[Mann Co. Engineer]";
			mes "You want to enchant your " + getitemname(.@Item) + "?";
			mes "The cost of this work is ^0000FF200 Battle Badges, and 100 Kafra Points^000000.";
			mes "I must warn you of the risk.";
			mes "Once the Accesory receives the enchant ^FF0000it will loose cards^000000.";
			mes "Do you still want to Enchant?";
			next;
			if ( select("^0000FFEnchant it^000000:Cancel") == 2 )
			{
				mes "[Mann Co. Engineer]";
				mes "Need some time to think about it, huh?";
				mes "Come back to me soon!";
				close;
			}

			if( countitem(7773) < 200 || ##KAFRAPOINTS < 100 )
			{
				mes "[Mann Co. Engineer]";
				mes "I'd like to go ahead with this Enchant attempt, but you're missing a few things.";
				mes "You sure that you have the badges?";
				close;
			}

			set .@Slot0, getequipcardid(.@Part,0);
			if( !getequipisequiped(.@Part) || .@Slot0 == 255 || .@Slot0 == 254 || .@Slot0 < 0 || .@Item != getequipid(.@Part) )
			{
				mes "[Mann Co. Engineer]";
				mes "This is not the equip we were talking about...";
				mes "Get out of here!!";
				close;
			}

			delitem 7773,200;
			paypoints 100;
			successenchant .@Part,4900;

			mes "[Mann Co. Engineer]";
			mes "Great, your accessory received ^0000FF" + getitemname(4900) + "^000000 enchant.";
			mes "It looks pretty well good!";
			mes "See you again, buddy!";
			close;
		case 7:
			mes "[Mann Co. Engineer]";
			mes "When an armor becomes enchanted, it will show a special hiding slot with a gem on it.";
			mes "This will grant additional stats to the armor, and with different values.";
			mes "You should know that the item will lose its refine, cards and previous enchants.";
			mes "And maybe the armor too, if I fail...";
			next;
			mes "[Mann Co. Engineer]";
			mes "However, only some body armors may be enchanted.";
			mes "Tell me what list do you want to explore...";
			next;
			deletearray .@Armor[0],127;
			switch( select( "Non Slotted Armors","Slotted Armors","High Class Armors" ) )
			{
				case 1: setarray .@Armor[0],2358,2307,2309,2314,2316,2321,2325,2327,2330,2332,2334,2335,2341,2344,2346,2348,2350,2337,2386; break;
				case 2: setarray .@Armor[0],2311,2318,2319,2320,2308,2310,2315,2317,2322,2324,2326,2331,2333,2336,2342,2345,2347,2349,2351; break;
				case 3: setarray .@Armor[0],2357,2364,2365,2374,2375,2387,2389,2391,2390,2376,2377,2378,2379,2380,2381,2382,2394; break;
			}
			
			mes "^0000FF** Armor List **^000000";
			for( set .@i, 0; .@i < getarraysize(.@Armor); set .@i, .@i + 1 )
				mes "" + getitemname(.@Armor[.@i]) + "";
			
			next;
			mes "[Mann Co. Engineer]";
			mes "Talk me again if you want to enchant your armor or if you need more information.";
			close;
		case 8:
			mes "[Mann Co. Engineer]";
			mes "Take it easy, adventurer.";
			mes "If you ever want to try to enchant your armors, come back and let me know.";
			close;
	}
}


bat_room,163,147,4	script	Wise Old Woman#bgroom	78,{
	set .@n$, "[Wise Old Woman]";
	
	if (islowratechar() == 1)
	{
		mes "This function is only available to BG chars!";
		close;
	}
	
	mes .@n$;
	mes "Hello there, I'm a woman recruited by Mann Co. for my special abilities. You see, I have the power to remove cards compounded onto eequipment. Would you like to try it?";
	next;
	switch (select("Of course!:How much does it cost?:No thanks."))
	{
		case 1:
			mes .@n$;
			mes "Aye, and what shall I examine for you?";
			next;
			
			setarray .@position$[1], "Head","Body","Left hand","Right hand","Robe","Shoes","Accessory 1","Accessory 2","Head 2","Head 3";
			set .@menu$,"";
			for( set .@i,1; .@i <= 10; set .@i,.@i+1 )
			{
				if( getequipisequiped(.@i) )
					set .@menu$, .@menu$ + .@position$[.@i] + "-" + "[" + getequipname(.@i) + "]";

				set .@menu$, .@menu$ + ":";
			}
			set .@part,select(.@menu$);
			if(!getequipisequiped(.@part)) {
				mes .@n$;
				mes "Young one... You're not wearing anything there that I can remove cards from.";
				close;
			}
			if(getequipisrental(.@part) == 1)
			{
				mes .@n$;
				mes "Ah.. it seems this item is a rental item, and as such... I cannot work on it. It must be returned as it was found.";
				close;
			}
			if(getequipcardcnt(.@part) == 0) {
				mes "[Wise Old Woman]";
				mes "Young one... There are no cards compounded on this item. I can do nothing with it, I'm afraid.";
				close;
			}
			set .@cardcount,getequipcardcnt(.@part);
			
			if (!checkweight(1202,(.@cardcount+1))) 
			{ 
				mes "^3355FFJust a minute!";
				mes "I can't offer any of my services to you because you're carrying too much stuff. Put your extra items in Kafra Storage and come again~";
				close;
			}
			
			mes .@n$;
			mes "This item has " + .@cardcount + " cards compounded on it. To perform my magic, I will need " + (50+(.@cardcount * 10)) + " Kafra Points";
			next;
			if(select("Very well. Do it.:Nevermind.") == 2) 
			{
				mes .@n$;
				mes "Very well. Return at once if you seek my services.";
				close;
			}
			if( ##KAFRAPOINTS < (50+(.@cardcount*10)) ) {
				mes .@n$;
				mes "You do not have all the items I require to work my magic, child. Come again when you do.";
				close;
			}
			paypoints(50+(.@cardcount*10));

			set .@failchance,rand(100);

			if(.@failchance < 10) 
			{
				next;
				failedremovecards .@part,3;				
				mes .@n$;
				mes "I have failed to remove the cards. Luckily, however, both the item and the cards are still okay.";
				close;	
			}
			next;
			successremovecards .@part;
			mes .@n$;
			mes "The process was a success. Here are your cards and your item. Farewell.";
			close;		
		case 2:
			mes .@n$;
			mes "I charge a flat fee of 50 Kafra Points, plus 10 Kafra Points for each card I remove from the item. Don't worry, Mann Co. has provided me plenty of Yellow Gemstones to use this magic. Their supply is practically infinite...";
			close;
		case 3:
			mes .@n$;
			mes "Very well. Return at once if you seek my services.";
			close;
	}

}

