//===== eAthena Script ======================================= 
//= Gym Pass Cash Item NPC
//===== By: ================================================== 
//= Kisuka
//===== Current Version: ===================================== 
//= 1.3
//===== Compatible With: ===================================== 
//= eAthena SVN
//===== Description: ========================================= 
//= [Aegis Conversion]
//= Exchange Gym Passes to learn "Enlarge Weight Limit"
//= iRO NPC situated in Payon beside Kafra Shop.
//===== Additional Comments: ================================= 
//= 1.0 First version [L0ne_W0lf]
//= 1.1 Fixed the NPC saying you "00" [L0ne_W0lf]
//= 1.2 Replaced effect numerics with constants. [Samuray22]
//= 1.3 Updated script, and fixed potential errors. [L0ne_W0lf]
//============================================================ 

payon,173,141,4	script	Ripped Cabus#GymPass	899,{
	mes "[Ripped]";
	mes "Hey, train with me, and I can";
	mes "guarantee that you'll be able";
	mes "to lift and carry more of your";
	mes "stuff. Just gimme your";
	mes "^FF0000Gym Pass^000000 each time,";
	mes "and we'll be good to go.";
	switch(select("Yes:No:Um, my workouts wore off.")) {
	case 1:
		if (gympassmemory < 10) {
			set .@add_carry,gympassmemory + 1;
			set .@remain_carry,10 - .@add_carry;
			if (countitem(7776) > 0) {
				specialeffect2 EF_EARTHSPIKE;
				specialeffect2 EF_DEVIL;
				specialeffect2 EF_COIN;
				specialeffect2 EF_SIGHTRASHER;
				next;
				mes "[Ripped]";
				mes "There, you should be able";
				mes "to carry more stuff with you.";
				mes "Let's see, we can increase";
				mes "your item carrying capacity";
				mes "^FF00000" + .@remain_carry + "^000000 more times if we continue";
				mes "training together like this.";
				delitem 7776,1; //Max_Weight_Up_Scroll
				set gympassmemory,.@add_carry;
				skill "ALL_INCCARRY",.@add_carry,0;
				close;
			}
			else {
			next;
				mes "[Ripped]";
				mes "Dude, what'd I tell you?";
				mes "You gotta bring me your";
				mes "^FF0000Gym Pass^000000 if you wanna";
				mes "work out, and build up your";
				mes "item carrying muscles.";
				close;
			}
		}
		else {
			next;
			mes "[Ripped]";
			mes "Dude, I don't think we can";
			mes "build up your item carrying";
			mes "muscles anymore than that.";
			mes "It's too dangerous for your";
			mes "body if we even tried! C'mon,";
			mes "I told you about the limits.";
			close;
		}
	case 2:
		next;
		mes "[Ripped]";
		mes "Aw, that's too bad.";
		mes "Well, come back if you";
		mes "change your mind. Tell";
		mes "your friends about me:";
		mes "if they're flabby, I'll help";
		mes "get them in shape.";
		close;
	case 3:
		if (gympassmemory > 0) {
			next;
			mes "[Ripped]";
			mes "What happened?";
			mes "You let your item carrying";
			mes "muscles just atrophy? Lucky";
			mes "for you, there's such a thing";
			mes "as muscle memory. It's won't take";
			mes "as long to build 'em back up...";
			next;
			specialeffect2 EF_EARTHSPIKE;
			specialeffect2 EF_DEVIL;
			specialeffect2 EF_COIN;
			specialeffect2 EF_SIGHTRASHER;
			mes "[Ripped]";
			mes "How about that?";
			mes "Your item carrying";
			mes "muscles grew back,";
			mes "just like that! Try not to";
			mes "wimp out again, okay?";
			skill "ALL_INCCARRY",gympassmemory,0;
			close;
		}
		else {
			next;
			mes "[Ripped]";
			mes "Uhh...";
			mes "We didn't work out";
			mes "together before.";
			mes "I'm sure about that.";
			close;
		}
	}
}
