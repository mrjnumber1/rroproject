//===== eAthena Script ======================================= 
//= Stylist
//===== By: ================================================== 
//= Saithis
//===== Credits: =============================================
//= eAthena Dev team (some of the text)
//===== Current Version: ===================================== 
//= 1.2
//===== Compatible With: ===================================== 
//= eAthena SVN 
//===== Description: ========================================= 
//= Revised dye NPC
//===== TODO: ================================================
// - Implement price for look change
//===== Additional Comments: =================================
//= v1.0 - First release [Saithis]
//= v1.1 - The NPC remebers now the direction (keep going - go back) [Saithis]
//= v1.2 - Use getbattleflag to get max values [Saithis]
//============================================================ 

function	script	F_stylist	{
	set .@style,0;
	set .@name$,"[Stylist]";
	setarray .@max[0], getbattleflag("max_cloth_color"), getbattleflag("max_hair_color"), getbattleflag("max_hair_style");
	setarray .@type[0],7,6,1;
	
	next;
	mes .@name$;
	mes "I can change your appearance for you, if you'd like";
	mes "What would you like to change?";
	set .@s, select("Clothes Color:Hair Color:Hair Style")-1;
	next;
	mes .@name$;
	mes "How would you like to search?";
	set .@start, select("From what I have:From the beginning:Choose where to start");

	if (.@start==3)
	{
		next;
		mes .@name$;
		mes "Choose a style between ^0000FF0^000000 and ^0000FF" +.@max[.@s]+"^000000.";
		input .@style;
		if (.@style < 0 || .@style > .@max[.@s])
		{
			mes .@name$;
			mes "Style out of range!";
			close;
		}
	}
	else if (.@start == 2)
		set .@style, 0;
	else
		set .@style, getlook(.@type[.@s]);

	next;
	mes .@name$;
	mes "Okay, here we go; starting at style ^007700"+.@style+"^000000";
	next;
	set .@previous, 0;
	set .@menureset, 1;
	
	while(.@style != -1)
	{
		setlook(.@type[.@s], .@style);
		mes "This is style number ^007700" + .@style + "^000000.";
		set .@next, .@style+1;
		set .@prev, .@style-1;
		
		if (.@style == .@max[.@s])
		{
			set .@next,0;
			mes "<<Limit Reached>>";
		}
		if (.@style == 0)
		{
			set .@prev, .@max[.@s];
			mes "<<Beginning Reached>>";
		}
		
		
		if (.@previous)
		{
			set .@menustr$, "Previous - "+.@prev+":Next - "+.@next+":Jump:I'm done"; 
		}
		else
		{
			set .@menustr$, "Next - "+.@next+":Previous - "+.@prev+":Jump:I'm done";
		}
		
		
		switch(select(.@menustr$))
		{
			case 1: //prev/next
				if(.@previous) // this means we go to PREV
				{
					set .@style, .@prev;
				}
				else  //this means we go to NEXT
				{
					set .@style, .@next;
				}
				break;
			case 2: //prev/next
				if(.@previous) //this means we go to NEXT
				{
					set .@previous, 0;
					set .@style, .@next;
				}
				else // this means we go to PREV
				{
					set .@previous, 1;
					set .@style, .@prev;
				}
				break;
			case 3: // jump to
			
				next;
				mes .@name$;
				mes "Where would you like to jump?";
				mes "Between ^0077000^000000 and ^007700"+.@max[.@s]+"^000000.";
				input .@style;
				if (.@style < 0 || .@style > .@max[.@s]) 
				{
					next;
					mes .@name$;
					mes "Style is out of range!";
					close;
				}
				break;
			case 4:
			
				next;
				mes .@name$;
				mes "You look great!";
				set .@style, -1;
				close;
				break;
		}
	}
	
	close;
}